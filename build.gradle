plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group = 'com.karasu256'

def versionFile = file('version.properties')
def versionProps = new Properties()

// バージョンプロパティとビルド番号の初期化
if (versionFile.exists()) {
    versionFile.withInputStream { versionProps.load(it) }
} else {
    // 初期バージョンを設定
    versionProps.setProperty('major', '0')
    versionProps.setProperty('minor', '0')
    versionProps.setProperty('patch', '1')
    versionFile.createNewFile()
    versionFile.withOutputStream { versionProps.store(it, "Version properties for ${project.name}") }
}

def buildNumberFile = file('build-number.properties')
def buildProps = new Properties()

if (buildNumberFile.exists()) {
    buildNumberFile.withInputStream { buildProps.load(it) }
} else {
    buildProps.setProperty('buildNumber', '0')
    buildNumberFile.createNewFile()
    buildNumberFile.withOutputStream { buildProps.store(it, "Build Number for ${project.name}") }
}

def majorVersion = versionProps.getProperty('major').toInteger()
def minorVersion = versionProps.getProperty('minor').toInteger()
def patchVersion = versionProps.getProperty('patch').toInteger()
def buildNumber = buildProps.getProperty('buildNumber').toInteger()

// バージョン形式
version = "${majorVersion}.${minorVersion}.${patchVersion}.${buildNumber}"

// Minecraft（MC）バージョンを定義
def mcVersion = "1.21.3"

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.3-R0.1-SNAPSHOT")
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = 'karasu-command-api'
            
            version = project.version.toString()
            
            pom {
                name = 'KarasuCommandAPI'
                description = 'A Minecraft plugin that provides command API'
                url = 'https://github.com/Karasu-Lab/KarasuCommandAPI'
                
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                
                developers {
                    developer {
                        id = 'hashibutogarasu'
                        name = 'Hashibutogarasu'
                        email = 'karasu@karasu256.com'
                        url = 'https://github.com/Hashibutogarasu'
                    }
                }
                
                scm {
                    connection = 'scm:git:git://github.com/Karasu-Lab/KarasuCommandAPI.git'
                    developerConnection = 'scm:git:ssh://github.com/Karasu-Lab/KarasuCommandAPI.git'
                    url = 'https://github.com/Karasu-Lab/KarasuCommandAPI'
                }
            }
        }
    }
    repositories {
        maven {
            name = "Local"
            url = layout.buildDirectory.dir('repos/bundles')
        }
        
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/Karasu-Lab/KarasuCommandAPI")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

tasks.withType(PublishToMavenRepository) {
    mustRunAfter tasks.withType(Sign)
}

// ルートプロジェクトの共通タスクを適用
rootProject.ext.createCommonTasks(project)

tasks.named('build') {
    dependsOn 'sourcesJar', 'javadocJar', 'syncVersionWithGit'
    finalizedBy 'incrementBuildNumber'
}

tasks.named('processResources') {
    dependsOn 'updatePluginVersion'
}

tasks.named('updatePluginVersion') {
    dependsOn 'syncVersionWithGit'
}