plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id 'maven-publish'
    id 'signing'
    id 'org.danilopianini.publish-on-central' version '8.0.6'
}

group = 'com.karasu256'

def versionFile = file('version.properties')
def versionProps = new Properties()

def initializeWithGitVersion() {
    def gitVersion = fetchLatestGitVersion()
    versionProps.setProperty('major', gitVersion[0].toString())
    versionProps.setProperty('minor', gitVersion[1].toString())
    versionProps.setProperty('patch', gitVersion[2].toString())
    println "Initialized version from Git: ${gitVersion[0]}.${gitVersion[1]}.${gitVersion[2]}"
    return gitVersion
}

if (versionFile.exists()) {
    versionFile.withInputStream { versionProps.load(it) }
} else {
    def gitVersion = initializeWithGitVersion()
    versionFile.createNewFile()
    versionFile.withOutputStream { versionProps.store(it, "Version properties for ${project.name}") }
}

def buildNumberFile = file('build-number.properties')
def buildProps = new Properties()

if (buildNumberFile.exists()) {
    buildNumberFile.withInputStream { buildProps.load(it) }
} else {
    buildProps.setProperty('buildNumber', '0')
    buildNumberFile.createNewFile()
}

def majorVersion = versionProps.getProperty('major').toInteger()
def minorVersion = versionProps.getProperty('minor').toInteger()
def patchVersion = versionProps.getProperty('patch').toInteger()
def buildNumber = buildProps.getProperty('buildNumber').toInteger()

version = "${majorVersion}.${minorVersion}.${patchVersion}-build.${buildNumber}"

def fetchLatestGitVersion() {
    def latestVersion = [0, 0, 1] // デフォルトバージョンを0.0.1に変更
    try {
        def process = "git ls-remote --tags https://github.com/Hashibutogarasu/KCommandAPI.git".execute()
        def tags = process.text.readLines()
            .collect { it.split()[1].replaceAll("refs/tags/v", "") }
            .findAll { it =~ /^\d+\.\d+\.\d+$/ }
        
        if (tags) {
            def latest = tags.last()
            latestVersion = latest.tokenize('.').collect { it.toInteger() }
            println "Found latest version tag: v${latest}"
            
            // 現在のバージョンと新しいバージョンを比較
            def currentVersion = [
                versionProps.getProperty('major', '0').toInteger(),
                versionProps.getProperty('minor', '0').toInteger(),
                versionProps.getProperty('patch', '0').toInteger()
            ]
            
            if (latestVersion != currentVersion) {
                println "Version changed from ${currentVersion.join('.')} to ${latestVersion.join('.')}"
                // ビルドナンバーをリセット
                buildProps.setProperty('buildNumber', '0')
                buildProps.store(buildNumberFile.newWriter(), null)
            }
        } else {
            println "No version tags found. Using default version: ${latestVersion.join('.')}"
        }
    } catch (Exception e) {
        println "Warning: Failed to fetch version from Git: ${e.message}"
        println "Using default version: ${latestVersion.join('.')}"
    }
    return latestVersion
}

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.3-R0.1-SNAPSHOT")
}

tasks {
    runServer {
        // Configure the Minecraft version for our task.
        // This is the only required configuration besides applying the plugin.
        // Your plugin's jar (or shadowJar if present) will be used automatically.
        minecraftVersion("1.21")
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    withJavadocJar()
    withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

publishOnCentral {
    repoOwner = 'Hashibutogarasu'
    projectDescription = 'A Minecraft plugin that provides command API'

    mavenCentral.user.set(findProperty("mavenCentral.user") ?: System.getenv("MAVEN_CENTRAL_USER"))
    mavenCentral.password.set(findProperty("mavenCentral.password") ?: System.getenv("MAVEN_CENTRAL_PASSWORD"))
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = 'kcommandapi'
            
            pom {
                name = 'KCommandAPI'
                description = 'A Minecraft plugin that provides command API'
                url = 'https://github.com/Hashibutogarasu/KCommandAPI'
                
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                
                developers {
                    developer {
                        id = 'hashibutogarasu'
                        name = 'Hashibutogarasu'
                        email = 'karasu@karasu256.com'
                        url = 'https://github.com/Hashibutogarasu'
                    }
                }
                
                scm {
                    connection = 'scm:git:git://github.com/Hashibutogarasu/KCommandAPI.git'
                    developerConnection = 'scm:git:ssh://github.com/Hashibutogarasu/KCommandAPI.git'
                    url = 'https://github.com/Hashibutogarasu/KCommandAPI'
                }
            }
        }
    }
    
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/Hashibutogarasu/KCommandAPI")
            credentials {
                username = findProperty('gpr.user') ?: System.getenv("GITHUB_ACTOR")
                password = findProperty('gpr.key') ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

signing {
    def signingKeyId = findProperty("signing.keyId")
    def signingPassword = findProperty("signing.password")
    def signingSecretKeyRingFile = findProperty("signing.secretKeyRingFile")
    
    required { gradle.taskGraph.hasTask("publish") }
    
    if (signingKeyId && signingPassword && signingSecretKeyRingFile) {
        def keyFile = new File(signingSecretKeyRingFile.toString().replaceAll('"', ''))
        useInMemoryPgpKeys(signingKeyId, keyFile.text, signingPassword)
    } else {
        println "署名情報が不完全です - 署名はスキップされます"
    }
}

tasks.withType(PublishToMavenRepository) {
    mustRunAfter tasks.withType(Sign)
}

tasks.register('buildJar', Jar) {
    archiveBaseName.set('KCommandAPI')
    from sourceSets.main.output
    dependsOn configurations.compileClasspath
}

tasks.named('build') {
    dependsOn 'sourcesJar', 'javadocJar', 'syncVersionWithGit'
    finalizedBy 'signMavenJavaPublication', 'incrementBuildNumber'
}

tasks.register('incrementBuildNumber') {
    group = 'build'
    description = 'Increment build number after successful build'
    
    doLast {
        def currentBuildNumber = buildProps.getProperty('buildNumber').toInteger()
        buildProps.setProperty('buildNumber', (currentBuildNumber + 1).toString())
        buildNumberFile.withOutputStream { buildProps.store(it, null) }
        println "Build number incremented to: ${currentBuildNumber + 1}"
    }
}

tasks.register('updatePluginVersion') {
    group = 'build'
    description = 'Update plugin.yml version from project version'
    
    doLast {
        def pluginYml = file('src/main/resources/plugin.yml')
        def lines = pluginYml.readLines()
        def updatedLines = lines.collect { line ->
            if (line.trim().startsWith('version:')) {
                return "version: '${project.version}'"
            }
            return line
        }
        pluginYml.text = updatedLines.join('\n')
    }
}

tasks.named('processResources') {
    dependsOn 'updatePluginVersion'
}

tasks.register('syncVersionWithGit') {
    group = 'build'
    description = 'Synchronize version with GitHub release tags'
    
    doLast {
        def gitVersion = fetchLatestGitVersion()
        versionProps.setProperty('major', gitVersion[0].toString())
        versionProps.setProperty('minor', gitVersion[1].toString())
        versionProps.setProperty('patch', gitVersion[2].toString())
        versionFile.withOutputStream { versionProps.store(it, "Version properties for ${project.name}") }
        
        // バージョンプロパティの再読み込み
        project.version = "${gitVersion[0]}.${gitVersion[1]}.${gitVersion[2]}-build.${buildNumber}"
        println "Version synchronized with Git: ${project.version}"
    }
}

tasks.named('build') {
    dependsOn 'sourcesJar', 'javadocJar', 'syncVersionWithGit'
    finalizedBy 'signMavenJavaPublication', 'incrementBuildNumber'
}
